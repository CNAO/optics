! define all observations
writeObservations(fileName,QuadName,myDipName,myODipName,MonName): macro{
   assign, echo=fileName;
   PRINTF, TEXT="% 19.12E,% 6.1f,% 19.12E,% 19.12E,% 19.12E,% 19.12E,% 19.12E,% 19.12E,% 12.5E,% 12.5E,% 12.5E,% 12.5E,% 12.5E,% 12.5E",
           VALUE=Brho, BP, 
           table(twiss,MonName,x), table(twiss,MonName,betx), table(twiss,MonName,dx),
           table(twiss,MonName,y), table(twiss,MonName,bety), table(twiss,MonName,dy),
           I_QuadName, QuadName->K1, I_myDipName, myDipName->K0*IDB_LENGTH, I_myODipName, myODipName->K0*IDB_LENGTH;
   assign, echo=terminal;
};
! a useful header to file written by writeObservations(fileName,MonName) macro
writeHeader(fileName,QuadName,myDipName,myODipName,MonName): macro{
   assign, echo=fileName;
   PRINT, TEXT=# Brho[Tm], BP[mm], X:MonName[m], BETX:MonName[m], DX:MonName[m], Y:MonName[m], BETY:MonName[m], DY:MonName[m], I:QuadName[A], K1:QuadName[m-2], I:myDipName[A], K0L:myDipName[rad], I:myODipName[A], K0L:myODipName[rad];
   assign, echo=terminal;
};
! the actual macro:
compute_Scans(myQuaName,myDipName,myODipName,myMonName,fileName): macro={
   REMOVEFILE, FILE=fileName;
   exec, writeHeader(fileName,myQuaName,myDipName,myODipName,myMonName);
   
   ! loop through dipole current values
   I_myDipName=IminD;
   I_myODipName=IminDo;
   while (I_myDipName<=ImaxD) {
      ! - scanning dipole
      K0_orig=myDipName->K0;
      F_myDipName=1.0;
      exec, I2K_MEBT_DIPs_lin(myDipName,Brho);
      myDipName->K0=K0L_myDipName/IDB_LENGTH;
      ! - other dipole
      K0_origO=myODipName->K0;
      F_myODipName=1.0;
      exec, I2K_MEBT_DIPs_lin(myODipName,Brho);
      myODipName->K0=K0L_myODipName/IDB_LENGTH;
      
      ! loop through quadrupole current values
      K1_orig=K1_myQuaName;
      I_myQuaName=IminQ;
      while (I_myQuaName<=ImaxQ) {
         ! get ready for this step
          exec, I2K_MEBT_QUs_lin(myQuaName,Brho);
         ! compute optics and save data
         use,sequence=MUXL;
         select,flag=twiss,clear;
         twiss, beta0=initial, centre=true;
         exec, writeObservations(fileName,myQuaName,myDipName,myODipName,myMonName);
         ! get ready for new step
         I_myQuaName=I_myQuaName+IstpQ;   ! get ready for new point
      };
      K1_myQuaName=K1_orig;             ! reset kick to original value
      myDipName->K0=K0_orig;            ! 
      myODipName->K0=K0_origO;          ! 
      I_myDipName=I_myDipName+IstpD;    ! get ready for new point
      I_myODipName=I_myODipName+IstpDo; ! 
  };
};

! first scan:
! - quad: M1_016A_QIB
! - dipole: M2_001A_IDB
! - screen: M2_014A_PIB
Idelta=15.0; ITM=-46.5;
IminQ=ITM-Idelta; ImaxQ=ITM+Idelta; IstpQ=0.5;
Idelta=5.0; ITM=125.5;
IminD=ITM-Idelta; ImaxD=ITM+Idelta; IstpD=1.0;
Idelta=0.0; ITM=125.93;
IminDo=ITM-Idelta; ImaxDo=ITM+Idelta; IstpDo=0.0;
exec, compute_Scans(M1_016A_QIB,M2_001A_IDB,M3_001A_IDB,M2_014A_PIB,M2_scan.tfs);

! second scan:
! - quad: M2_009A_QIB
! - dipole: M3_001A_IDB
! - screen: M3_004A_PIB
Idelta=15.0; ITM=25.0;
IminQ=ITM-Idelta; ImaxQ=ITM+Idelta; IstpQ=0.5;
Idelta=5.0; ITM=125.93;
IminD=ITM-Idelta; ImaxD=ITM+Idelta; IstpD=1.0;
Idelta=0.0; ITM=125.5;
IminDo=ITM-Idelta; ImaxDo=ITM+Idelta; IstpDo=0.0;
exec, compute_Scans(M2_009A_QIB,M3_001A_IDB,M2_001A_IDB,M3_004A_PIB,M3_scan.tfs);

! third scan
! - quad: M1_016A_QIB
! - dipole2: M2_001A_IDB and M3_001A_IDB
! - screen: M3_004A_PIB
Idelta=15.0; ITM=-46.5;
IminQ=ITM-Idelta; ImaxQ=ITM+Idelta; IstpQ=0.5;
Idelta=5.0; ITM=125.5;
IminD=ITM-Idelta; ImaxD=ITM+Idelta; IstpD=1.0;
Idelta=5.0; ITM=125.93;
IminDo=ITM-Idelta; ImaxDo=ITM+Idelta; IstpDo=1.0;
exec, compute_Scans(M1_016A_QIB,M2_001A_IDB,M3_001A_IDB,M3_004A_PIB,M2M3_scan.tfs);
