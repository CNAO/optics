! A.Mereghetti, 2021-01-19
! a file collecting MADX macros

! general aperture outside MBs: 145 mm x 74 mm (centred in 0,0)
!   wrap this aperture marker around thick elements;
aper1=145E-3/2; ! [m]
aper2=74E-3/2;  ! [m]
aper3=0.0;      ! [m]
aper4=0.0;      ! [m]
CNAO_APE: MARKER, APERTYPE=RECTANGLE, APERTURE={aper1,aper2,aper3,aper4};
! longitudinal shift of added apertures
tmpD=0.0; ! [m]

! add aperture markers upstream/downstream of a given element
! NB: aperType and aper1,aper2,aper3,aper4 and offx,offy should be defined before calling the macro;
addAperture(eleName): macro = {
   eleName, APERTYPE=apeType, APERTURE={aper1,aper2,aper3,aper4}, APER_OFFSET={offx,offy};
   eleName_EN: MARKER, APERTYPE=apeType, APERTURE={aper1,aper2,aper3,aper4}, APER_OFFSET={offx,offy};
   eleName_EX: MARKER, APERTYPE=apeType, APERTURE={aper1,aper2,aper3,aper4}, APER_OFFSET={offx,offy};
   SEQEDIT, sequence=MUXL;
   Install, element=eleName_EN, at=-0.5*eleName->L-  tmpD, from=eleName;
   Install, element=CNAO_APE  , at=-0.5*eleName->L-2*tmpD, from=eleName;
   Install, element=CNAO_APE  , at= 0.5*eleName->L+2*tmpD, from=eleName;
   Install, element=eleName_EX, at= 0.5*eleName->L+  tmpD, from=eleName;
   ENDEDIT;
};

! save a .tfs file with linear optics functions in a specific column order
saveOpticsTFS(fileName): macro = {
   select,flag=twiss,clear;
   select,flag=twiss,column=name,keyword,l,s,betx,alfx,bety,alfy,x,px,y,py,dx,dpx,dy,dpy,mux,muy;
   if (is_thin>0) {
      twiss,file="fileName_twiss_thin.tfs";
   } else {
      twiss,file="fileName_twiss.tfs";
   };
};

! save a .tfs file with geometry infos (strengths, aperture, etc...) in a specific column order
saveGeometryTFS(fileName): macro = {
   select,flag=twiss,clear;
   select,flag=twiss,column=name,keyword,l,s,kick,hkick,vkick,angle,k0l,k1l,k2l,apertype,aper_1,aper_2,aper_3,aper_4,apoff_1,apoff_2;
   if (is_thin>0) {
      twiss,file="fileName_geometry_thin.tfs";
   } else {
      twiss,file="fileName_geometry.tfs";
   };
};