! S.Savazzi, A.Mereghetti, 2021-01-20
! loop through the table used for generating ramps and see how much current
!    is needed to a specific family to distort Q1 by a given amount

! set reference K0 of main bends (possible matching)
K0MB_ref=K0MB;
! remove output file
REMOVEFILE, FILE="ks.txt";

! parse table
READTABLE, file="KmachinephotoCarbRFKO.csv", table=table_settings;
lunghezza=table(table_settings,tablelength);
Nstart=1;
Nstop=1;

n=Nstart;
WHILE (n <= Nstop ) {

SETVARS, TABLE=table_settings, ROW=n;
call, file="settings_from_table.str";
K0MB=K0MB_ref;

! notify user where we are
PRINTF, TEXT="at Ek = %g MeV/u- BP = %g mm - row = %.0f ...", VALUE= Ek,BP,n;

exec, setRigidity();

! set injection bumper k from the specified current
exec, I2K_InjBump(); ! input: Ibumper[A],Brho[Tm];

! set sextupole families from the specified current
exec, I2K_sextupoles_S6_SR(); ! input: Is_SR[A],Brho[Tm];
exec, I2K_sextupoles_S8_S1(); ! input: Is_SR[A],Brho[Tm];
exec, I2K_sextupoles_S9_S0(); ! input: Is_SR[A],Brho[Tm];


use,sequence=MUXL;

exec, saveOpticsTFS(extraction);
exec, saveGeometryTFS(extraction);

k_ini=KD;
delta_Q=5E-4;
USE, sequence=MUXL;
MATCH, sequence=MUXL;
VARY, NAME=KD, STEP=0.000001;
GLOBAL, SEQUENCE=MUXL, Q1=table(summ,Q1)+delta_Q;
LMDIF, CALLS=1000, TOLERANCE=1.0E-12;
!JACOBIAN, CALLS=100, TOLERANCE=1.0E-6;
ENDMATCH;
k_fin=KD;

exec, saveOpticsTFS(extraction_matched);
exec, saveGeometryTFS(extraction_matched);

assign, echo="k.txt";
PRINTF, TEXT="%.12f %.12f", VALUE= k_ini,k_fin;
n = n+1;
assign, echo=terminal;
};
