! convert current into K, using a fifth order polynomial
I2K_HEBT_QUs_fifth(MagName,biro) : macro {
  ! the macro expects IMagName, KMagName and FMagName to be defined
  ! eg. QH2_012A -> IQH2_012A, KQH2_012A, FQH2_012A
  KMagName = (-2.794551e-1
               +9.32005e-2   *abs(IMagName)
               -4.26890e-4   *abs(IMagName)^2
               +3.498485e-6  *abs(IMagName)^3
               -1.1996845e-8 *abs(IMagName)^4
               +1.3348656E-11*abs(IMagName)^5) /biro *abs(FMagName);
  if (IMagName<0){ KMagName=-KMagName; };
};

! convert current into K, using a linear dependence
I2K_HEBT_QUs_lin(MagName,biro) : macro {
  ! the macro expects IMagName, KMagName and FMagName to be defined
  ! eg. QH2_012A -> IQH2_012A, KQH2_012A, FQH2_012A
  ! gradiente integrato GL(T) = 3.258e-02 * I (A)
  ! lq = 0.45 m
  ! fromItoK = GL/(lq*Bro) 
  KMagName := 0.03258/(0.45*biro) *IMagName *abs(FMagName);
};

I2K_HEBT_CEBH(MagName,biro): macro {
  ! from SSavazzi, relation for MADbuilder
  ! the macro expects IMagName, KMagName and FMagName to be defined
  ! Lmag=0.52m;
  KMagName := ((2.615E-5-3.385E-4*IMagName)/biro*0.52)*abs(FMagName);
};

I2K_HEBT_CEBV(MagName,biro): macro {
  ! from SSavazzi, relation for MADbuilder
  ! the macro expects IMagName, KMagName and FMagName to be defined
  ! Lmag=0.52m;
  KMagName := ((2.981E-5-3.385E-4*IMagName)/biro*0.52)*abs(FMagName);
};

save_strengths_particular(): macro {
   print, TEXT="";
   print, TEXT="! main dipole strengths [rad/m]";
   value, K0MB;
   print, TEXT="";
   print, TEXT="! quadrupole strengths [rad/m^2]";
   value, KQH2_012A;
   value, KQH2_016A;
   value, KQH2_022A;
   value, KQH4_003A;
   value, KQH4_007A;
   value, KQH4_013A;
   value, KQH5_005A;
   value, KQH5_009A;
   value, KQH5_015A;
   value, KQT1_004A;
   value, KQT1_013A;
   value, KQT1_019A;
   value, KQT2_005A;
   value, KQT2_012A;
   value, KQT2_018A;
};

compute_RM(myName,myKickField,myI2K): macro = {
  imyName=currI;
  kmyName_orig=myName->myKickField;
  fmyName=1.0;

  exec, myI2K(myName,Brho);
  ! value, myName->myKickField;
  myName->myKickField=kmyName;
  ! value, myName->myKickField;
  
  use,sequence=APICLS009;
  select,flag=twiss,clear;
  twiss, beta0=initial;
  if (iLine==1) {
    xSFH=table(twiss,t2_021b_sfh,x);
    ySFH=table(twiss,t2_021b_sfh,y);
    xNZL=table(twiss,t2_030b_nzl,x);
    yNZL=table(twiss,t2_030b_nzl,y);
    xISO=table(twiss,t2_032a_mob,x);
    yISO=table(twiss,t2_032a_mob,y);
  }elseif(iLine==2) {
    xSFH=table(twiss,u2_019b_sfh,x);
    ySFH=table(twiss,u2_019b_sfh,y);
    xNZL=table(twiss,u2_027b_nzl,x);
    yNZL=table(twiss,u2_027b_nzl,y);
    xISO=table(twiss,u2_029a_mob,x);
    yISO=table(twiss,u2_029a_mob,y);
  }elseif(iLine==3) {
    xSFH=table(twiss,v2_019b_sfh,x);
    ySFH=table(twiss,v2_019b_sfh,y);
    xNZL=table(twiss,v2_029b_nzl,x);
    yNZL=table(twiss,v2_029b_nzl,y);
    xISO=table(twiss,v2_031a_mob,x);
    yISO=table(twiss,v2_031a_mob,y);
  }elseif(iLine==4) {
    xSFH=table(twiss,z2_021b_sfh,x);
    ySFH=table(twiss,z2_021b_sfh,y);
    xNZL=table(twiss,z2_030b_nzl,x);
    yNZL=table(twiss,z2_030b_nzl,y);
    xISO=table(twiss,z2_032a_mob,x);
    yISO=table(twiss,z2_032a_mob,y);
  };
  ! value, myName->HKICK;
  ! value, myName->VKICK;
  
  ! use,sequence=APICLS009;
  ! exec, saveOptics(myName_myKickField);

  ! reset original kick
  ! value, myName->myKickField;
  myName->myKickField=kmyName_orig;
  ! value, myName->myKickField;
};

parseTable(): macro={
   if (iLine==1) {
      READTABLE, file="Protoni_Sala3_2021-02-13.tfs", table=table_settings;
   }elseif (iLine==2) {
      READTABLE, file="Protoni_Sala2H_2021-02-13.tfs", table=table_settings;
   }elseif (iLine==3) {
      READTABLE, file="Protoni_Sala2V_2021-02-13.tfs", table=table_settings;
   }elseif (iLine==4) {
      READTABLE, file="Protoni_Sala1_2021-02-13.tfs", table=table_settings;
   };
};

saveOptics(additional): macro={
  if (iLine==1) {
      exec, saveOpticsTFS(LineT_additional);
      exec, saveGeometryTFS(LineT_additional);
   }elseif (iLine==2) {
      exec, saveOpticsTFS(LineU_additional);
      exec, saveGeometryTFS(LineU_additional);
   }elseif (iLine==3) {
      exec, saveOpticsTFS(LineV_additional);
      exec, saveGeometryTFS(LineV_additional);
   }elseif (iLine==4) {
      exec, saveOpticsTFS(LineZ_additional);
      exec, saveGeometryTFS(LineZ_additional);
   };
};

saveStrengths(additional): macro={
  if (iLine==1) {
      exec, save_strengths(LineT_additional);
   }elseif (iLine==2) {
      exec, save_strengths(LineU_additional);
   }elseif (iLine==3) {
      exec, save_strengths(LineV_additional);
   }elseif (iLine==4) {
      exec, save_strengths(LineZ_additional);
   };
};

compute_RM_currents(magName): macro ={
   currI=Imin;
   while (currI<=Imax) {
      exec, compute_RM(magName,HKICK,I2K_HEBT_CEBH);
      xhSFH=xSFH;
      yhSFH=ySFH;
      xhNZL=xNZL;
      yhNZL=yNZL;
      xhISO=xISO;
      yhISO=yISO;
      kickh=kmagName;
      exec, compute_RM(magName,VKICK,I2K_HEBT_CEBV);
      xvSFH=xSFH;
      yvSFH=ySFH;
      xvNZL=xNZL;
      yvNZL=yNZL;
      xvISO=xISO;
      yvISO=yISO;
      kickv=kmagName;
      assign, echo=magName_RM.txt;
      PRINTF, TEXT="% 19.12E % 6.1f % 8.3f % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E % 19.12E",
              VALUE=Brho, BP, currI, kickh, kickv, xhISO, yhISO, xhNZL, yhNZL, xhSFH, yhSFH, xvISO, yvISO, xvNZL, yvNZL, xvSFH, yvSFH;
      assign, echo=terminal;
      currI=currI+Istep;
   };
};

compute_RM_allCurrents(magName): macro={
   REMOVEFILE, FILE=magName_RM.txt;
   nEnLevel=NEnLevelstart;
   WHILE (nEnLevel <= NEnLevelstop ) {
      ! loop throuth LGEN data
      
      SETVARS, TABLE=table_settings, ROW=nEnLevel;
      K0MB=K0MB_ref;
      call, file="settings_from_LGEN_table.str";
      ! Ek=Ek_MeVN*1.0;  ! [MeV/n]
      ! BP=range_mm; ! [mm] 
      ! exec, setRigidity();
      
      ! notify user where we are
      PRINTF, TEXT="==========================================================================================",VALUE=0.0;
      PRINTF, TEXT="at Ek = %g MeV/u - BP = %g mm - row = %.0f ...", VALUE= Ek,BP,nEnLevel;
      PRINTF, TEXT="==========================================================================================",VALUE=0.0;
      
      exec, compute_RM_currents(magName);

      nEnLevel = nEnLevel+1;
   };
};